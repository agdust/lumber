module            =  { SOI ~ (directive | clause)* ~ EOI }
external_handle   =  { SOI ~ atom ~ ("::" ~ atom)* ~ arity+ ~ EOI }

directive         =  { ":-" ~ instruction ~ "." }
instruction       =  { nat | mod_ | use_ | pub_ | mut_ | inc }
nat               =  { "nat" ~ "(" ~ handle ~ ")" }
mod_              =  { "mod" ~ "(" ~ atom ~ ")" }
use_              =  { "use" ~ "(" ~ multi_handle ~ ")" }
pub_              =  { "pub" ~ "(" ~ handle ~ ")" }
mut_              =  { "mut" ~ "(" ~ handle ~ ")" }
inc               =  { "inc" ~ "(" ~ handle ~ ")" }

multi_handle      =  { module_path ~ ("(" ~ handles ~ ")")? }
module_path       =  { scope | scope_prefix }

scope_prefix      =  { (up ~ ("::" ~ up)* | root | lib)  }
    up            =  { "^" }
    root          =  { "~" }
    lib           = ${ "@" ~ atom }

scope             =  { (scope_prefix ~ "::")? ~ atom ~ ("::" ~ atom)* }

handles           =  { (handle | alias) ~ ("," ~ (handle | alias))* }
alias             =  { "alias" ~ "(" ~ handle ~ "," ~ "as" ~ ":" ~ handle ~ ")" }
handle            =  { atom ~ arity+ }
arity             = ${ "/" ~ integer_10 | ":" ~ atom }

clause            =  { fact | rule | function }
fact              =  { head ~ "." }
rule              =  { head ~ ":-" ~ body ~ "." }
function          =  { function_head ~ "<-" ~ evaluation ~ "." }

head              =  { atom ~ ( "(" ~ fields ~ ")" )? }
function_head     =  { atom ~ "!" ~ ( "(" ~ fields ~ ")" )? }
body              =  { procession }
evaluation        =  { (assumption ~ ",")* ~ operation }

procession        =  { disjunction ~ ("!" ~ disjunction)* }
disjunction       =  { conjunction ~ (";" ~ conjunction)* }
conjunction       =  { implication ~ ("," ~ implication)* }
implication       =  { unification ~ ("->" ~ unification)* }
unification       =  { assumption | predicate | "(" ~ procession ~ ")" }
assumption        =  { pattern ~ "<-" ~ operation }
expression        =  { value | aggregation | "(" ~ operation ~ ")" }
operation         =  { expression ~ (operator ~ expression)* }
aggregation       =  { set_aggregation | list_aggregation }
set_aggregation   =  { "{" ~ aggregation_body ~ "}" }
list_aggregation  =  { "[" ~ aggregation_body ~ "]" }
aggregation_body  =  { pattern ~ ":" ~ procession }

call              =  { scope ~ "!" ~ ( "(" ~ fields ~ ")" )? }
predicate         =  { scope ~ ( "(" ~ fields ~ ")" )? }
struct_           =  { atom ~ ( "(" ~ fields ~ ")" )? }

fields            =  { field ~ ("," ~ field)* }
field             =  { named_field | bare_field }
named_field       =  { atom ~ ":" ~ pattern }
bare_field        =  { pattern }

atom              =  { bare_atom | quoted_atom }
bare_atom         = @{ &XID_START ~ (LOWERCASE_LETTER | OTHER_LETTER) ~ XID_CONTINUE* }
quoted_atom       = @{ PUSH("#"*) ~ "'" ~ (!("'" ~ PEEK) ~ ANY)+ ~ "'" ~ POP }

operator          =  { named_operator | symbolic_operator }
named_operator    = ${ "`" ~ scope ~ "`" }
symbolic_operator = ${ !"!" ~ !"->" ~ !"<-" ~ (operator_symbol+) }
operator_symbol   =  { "-" | "+" | "*" | "/" | "%" | "<" | ">" | "=" | "?" | "$" | "!" | "^" | "&" | "|" | "@" | "#" | "\\" }

variable          = @{ &XID_START ~ (UPPERCASE_LETTER | TITLECASE_LETTER) ~ XID_CONTINUE* }
wildcard          = @{ "_" ~ XID_CONTINUE* }

value             =  { literal | call | struct_ | variable | list | set }
pattern           =  { literal | struct_ | variable | list | set | wildcard }

literal           =  { decimal | integer | string }

integer           =  { integer_2 | integer_16 | integer_10 }
integer_10        = @{ ASCII_NONZERO_DIGIT ~ (ASCII_DIGIT)* | "0" }
integer_2         = @{ "0b" ~ ASCII_BIN_DIGIT+ }
integer_16        = @{ "0x" ~ ASCII_HEX_DIGIT+ }

decimal           =  { decimal_10 }
decimal_10        = ${ integer_10 ~ "." ~ integer_10 }

string            = @{ PUSH("#"*) ~ "\"" ~ (!("\"" ~ PEEK) ~ ANY)* ~ "\"" ~ POP }

list              =  { "[" ~ list_entries ~ ("|" ~ (list | variable | wildcard))? ~ "]" | "[" ~ "]" }
set               =  { "{" ~ list_entries ~ ("|" ~ (set | variable | wildcard))? ~ "}" | "{" ~ "}" }
list_entries      =  { pattern ~ ("," ~ pattern)* }

WHITESPACE        = _{ " " | "\t" | NEWLINE }
COMMENT           = _{ "//" ~ (!"\n" ~ ANY)* ~ NEWLINE | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
