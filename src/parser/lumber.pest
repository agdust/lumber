module            =  { SOI ~ (directive | clause)* ~ EOI }
external_handle   =  { SOI ~ atom ~ ("::" ~ atom)* ~ arity ~ EOI }
question          =  { SOI ~ body ~ EOI }

directive         =  { ":-" ~ instruction ~ "." }
instruction       =  { nat | mod_ | use_ | pub_ | mut_ | inc }
nat               =  { "nat" ~ "(" ~ handle ~ ")" }
mod_              =  { "mod" ~ "(" ~ atom ~ ")" }
use_              =  { "use" ~ "(" ~ multi_handle ~ ")" }
pub_              =  { "pub" ~ "(" ~ handle ~ ")" }
mut_              =  { "mut" ~ "(" ~ handle ~ ")" }
inc               =  { "inc" ~ "(" ~ handle ~ ")" }

multi_handle      =  { module_path ~ ("(" ~ handles ~ ")")? }
module_path       =  { scope | scope_prefix }

scope_prefix      =  { (up ~ ("::" ~ up)* | root | lib)  }
    up            =  { "^" }
    root          =  { "~" }
    lib           = ${ "@" ~ atom }

scope             =  { (scope_prefix ~ "::")? ~ atom ~ ("::" ~ atom)* }

handles           =  { (handle | alias) ~ ("," ~ (handle | alias))* }
alias             =  { "alias" ~ "(" ~ handle ~ "," ~ "as" ~ ":" ~ handle ~ ")" }
handle            =  { atom ~ arity }
arity             =  { "/" ~ integer_10 ~ (":" ~ atom ~ ("/" ~ !"0" ~ !"1" ~ integer_10)?)* }

clause            =  { fact | rule | function }
fact              =  { head ~ "." }
rule              =  { head ~ ":-" ~ body ~ "." }
function          =  { function_head ~ "<-" ~ evaluation ~ "." }

head              =  { atom ~ ( "(" ~ fields ~ ")" )? }
function_head     =  { atom ~ "!" ~ ( "(" ~ fields ~ ")" )? }
body              =  { disjunction }
evaluation        =  { (assumption ~ ",")* ~ operation }

disjunction       =  { conjunction ~ (";" ~ conjunction)* }
conjunction       =  { procession ~ ("," ~ procession)* }
procession        =  { unification ~ ("->" ~ unification)* }
unification       =  { assumption | predicate | "(" ~ disjunction ~ ")" }
assumption        =  { pattern ~ "<-" ~ operation }
expression        =  { value | aggregation | "(" ~ operation ~ ")" }
operation         =  { expression ~ (operator ~ expression)* }
aggregation       =  { set_aggregation | list_aggregation }
set_aggregation   =  { "{" ~ aggregation_body ~ "}" }
list_aggregation  =  { "[" ~ aggregation_body ~ "]" }
aggregation_body  =  { pattern ~ ":" ~ disjunction }

call              =  { scope ~ "!" ~ ( "(" ~ fields ~ ")" )? }
predicate         =  { scope ~ ( "(" ~ fields ~ ")" )? }
struct_           =  { atom ~ ( "(" ~ fields ~ ")" )? }

fields            =  { named_fields | bare_fields ~ ("," ~ named_fields)? }
named_fields      =  { named_field ~ ("," ~ named_field)* }
named_field       =  { atom ~ ":" ~ bare_fields }
bare_fields       =  { pattern ~ (!("," ~ pattern ~ ":") ~ "," ~ pattern)* }

atom              =  { bare_atom | quoted_atom }
bare_atom         = @{ &XID_START ~ (LOWERCASE_LETTER | OTHER_LETTER) ~ XID_CONTINUE* }
quoted_atom       = @{ PUSH("#"*) ~ "'" ~ (!("'" ~ PEEK) ~ ANY)+ ~ "'" ~ POP }

operator          =  { named_operator | symbolic_operator }
named_operator    = ${ "`" ~ scope ~ "`" }
symbolic_operator = ${ !"->" ~ !"<-" ~ (operator_symbol+) }
operator_symbol   =  { "-" | "+" | "*" | "/" | "%" | "<" | ">" | "=" | "?" | "$" | "!" | "^" | "&" | "|" | "@" | "#" | "\\" }

variable          = @{ &XID_START ~ (UPPERCASE_LETTER | TITLECASE_LETTER) ~ XID_CONTINUE* }
wildcard          = @{ "_" ~ XID_CONTINUE* }

value             =  { literal | call | struct_ | variable | list | record| set }
pattern           =  { literal | struct_ | variable | list | record | set | wildcard }

literal           =  { decimal | integer | string }

integer           =  { integer_2 | integer_16 | integer_10 }
integer_10        = @{ ASCII_NONZERO_DIGIT ~ (ASCII_DIGIT)* | "0" }
integer_2         = @{ "0b" ~ ASCII_BIN_DIGIT+ }
integer_16        = @{ "0x" ~ ASCII_HEX_DIGIT+ }

decimal           =  { decimal_10 }
decimal_10        = ${ integer_10 ~ "." ~ integer_10 }

string            = @{ PUSH("#"*) ~ "\"" ~ (!("\"" ~ PEEK) ~ ANY)* ~ "\"" ~ POP }

list              =  { "[" ~ list_entries ~ list_tail? ~ "]" | "[" ~ "]" }
list_tail         =  { "," ~ ".." ~ (list | variable | wildcard)? }
set               =  { "{" ~ list_entries ~ set_tail? ~ "}" | "{" ~ "}" }
set_tail          =  { "," ~ ".." ~ (set | variable | wildcard)? }
list_entries      =  { pattern ~ ("," ~ pattern)* }
record            =  { "{" ~ named_fields ~ record_tail? ~ "}" | "{" ~ ":" ~ "}" }
record_tail       =  { "," ~ ".." ~ (record | variable | wildcard)? }

WHITESPACE        = _{ " " | "\t" | NEWLINE }
COMMENT           = _{ line_comment | block_comment }
line_comment      = _{ "//" ~ (!"\n" ~ ANY)* ~ NEWLINE }
block_comment     = _{ "/*" ~ (block_comment | !"*/" ~ ANY)* ~ "*/" }
